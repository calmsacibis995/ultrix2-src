#! /bin/csh -f
#
# Install SPMS Software Project Management System
#
DESTROOT=
# pexec relies upon the PROJECT environment variable
# which MUST be set to the root directory of the project.
cd ..
setenv PROJECT $cwd

set PATH = (${DESTROOT}/usr/new ${DESTROOT}/usr/ucb ${DESTROOT}/bin ${DESTROOT}/usr/bin)

#install libraries
foreach p (libhash libpdb libslist libspms libtree)
	cd $PROJECT/src/lib/$p/src
	sed -e '/###/,$d' < Makefile > $$Mf
	mv $$Mf Makefile
	make DESTROOT=${DESTROOT} install
end

#install programs in /usr/new
cd $PROJECT/src/bin
foreach p (*)
	cd $PROJECT/src/bin/$p
	sed -e '/###/,$d' < Makefile > $$Mf
	mv $$Mf Makefile
	make DESTROOT=${DESTROOT} install
end
rehash

# reconstruct Makefile dependencies and update
pexec -Tupdate "mkmf; make update"

# clean up
pexec -Tclean "make clean"

#install manual pages in /usr/man/mann
cd $PROJECT/man/mann
foreach p (*)
	cp $p /usr/man/mann/$p:r.n
end

# create ~root^ and ~root^usr projects
cd /
mkproject -d -Tproject ~root^
cd /usr/src
mkproject -Nusr -Tproject ~root^.../usr/src > /dev/null << 'EOF'
/usr source
'EOF'

# connect spms project to ~root^usr
mkproject -d ~root^usr/new/spms

exit(0)
